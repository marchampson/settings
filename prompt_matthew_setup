# matthew prompt theme

prompt_matthew_help () {
  cat <<'EOF'
This prompt is color-scheme-able.  You can invoke it thus:

  prompt matthew [<color1> [<color2> [<color3>]]]

where the colors are for the user@host background, current working
directory, and current working directory if the prompt is split over
two lines respectively.  The default colors are blue, cyan and green.
This theme works best with a dark background.

Recommended fonts for this theme: nexus or vga or similar.  If you
don't have any of these, then specify the `plain' option to use 7-bit
replacements for the 8-bit characters.

And you probably thought matthew was overkill.
EOF
}

prompt_matthew_setup () {
  prompt_matthew_color1=${1:-'blue'}
  prompt_matthew_color2=${2:-'cyan'}
  prompt_matthew_color3=${3:-'green'}

  base_prompt="%{${fg[white]}%}%n@%m%{$reset_color%} "
  post_prompt="%{$reset_color%}"

  base_prompt_no_color=$(echo "$base_prompt" | perl -pe "s/%{.*?%}//g")
  post_prompt_no_color=$(echo "$post_prompt" | perl -pe "s/%{.*?%}//g")

  precmd  () { prompt_matthew_precmd }
  preexec () { }
}

prompt_matthew_precmd () {
  setopt noxtrace localoptions
  local base_prompt_expanded_no_color base_prompt_etc
  local prompt_length space_left

  base_prompt_expanded_no_color=$(print -P "$base_prompt_no_color")
  base_prompt_etc=$(print -P "$base_prompt%(4~|...|)%3~")
  prompt_length=${#base_prompt_etc}
  if [[ $prompt_length -lt 40 ]]; then
    path_prompt="%{$fg_bold[$prompt_matthew_color2]%}%(4~|...|)%3~%{$fg_bold[white]%}"
  else
    space_left=$(( $COLUMNS - $#base_prompt_expanded_no_color - 2 ))
    path_prompt="%{$fg_bold[$prompt_matthew_color3]%}%${space_left}<...<%~$prompt_newline%{$fg_bold_white%}"
  fi
  PS1="$base_prompt$path_prompt %# $post_prompt"
  PS2="$base_prompt$path_prompt %_> $post_prompt"
  PS3="$base_prompt$path_prompt ?# $post_prompt"
}

# prompt_matthew_setup "$@"
